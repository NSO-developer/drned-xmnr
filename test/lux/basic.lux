[doc Testing DrNED-XMNR integration with NCS and DrNED]

[include common.luxinc]
[global ned0_dir=/tmp/xmnr/ned0]
[global ned0_log=$ned0_dir/test/output.log]

[macro run-queues queues]
    [my shell=$LUX_SHELLNAME]
[shell ncs-cli]
    [invoke prepare-xmnr $queues]
[shell os]
    !rm -f $ned0_log
    !$queues && echo -n en || echo -n dis; echo abled
    ?(disabled|enabled)
    [my qtest=$1]
    !$queues && echo -n dis || echo -n en; echo abled
    ?(disabled|enabled)
    [my neg_qtest=$1]
    !tail -F $ned0_log
    -Commit queues are $neg_qtest
[shell ncs-cli]
    [invoke test-walk-states "empty subnet dhcp-ls time"]
[shell os]
    """?
    -----*
    .* - walk states
    -----*
    """
    ?Commit queues are $qtest
    ?====* 1 passed, .* ====*
    ~$_CTRL_C_
    ?SH-PROMPT:
[shell $shell]
[endmacro]

[macro check-states states]
    [my shell=$LUX_SHELLNAME]
[shell os]
    ~netconf-console --get -x /devices/device/drned-xmnr/state/states |
    ~sed -n 's%.*<state>\(.*\)</state>.*%\1%p' |
    !sort | xargs echo
    """?
    netconf-console --get .*
    $states$$
    SH-PROMPT
    """
[shell $shell]
[endmacro]

[shell os]
    [invoke ned-test-setup]
    !ncs-make-package --netconf-ned ../../yang ned
    [invoke shell-check]
    [timeout 10]
    !make -C ned/src all
    [invoke shell-check]
    [timeout]
    !cd ..
    [invoke start-ncs-netsim ned]
[shell ncs-cli]
    [invoke prepare-ncs ned0]

    [invoke prepare-xmnr true]
    !config dhcp default-lease-time 200s
    !commit
    ???Commit complete.
    !drned-xmnr state record-state state-name time format c-style overwrite true
    ???success
    !drned-xmnr state record-state state-name time
    ???failure state time already exists
    ~drned-xmnr state import-state-files
    ! file-path-pattern ../subnet.xml merge false overwrite true
    ???success
    ~drned-xmnr state import-state-files
    ! file-path-pattern ../dhcp-ls.cfg format nso-c-style merge false overwrite true
    ???success
[shell os]
    !cp -v ../subnet2.xml $ned0_dir/test/states/
    ??'../subnet2.xml' -> '$ned0_dir/test/states/subnet2.xml'
    ?SH-PROMPT
[shell ncs-cli]
    !drned-xmnr state record-state state-name subnet2
    ???failure state subnet2 already exists
    [invoke check-states "dhcp-ls empty subnet subnet2 time"]
    !drned-xmnr state check-states validate true
    ???success all states are consistent

    [loop queues true false]
        [invoke run-queues $queues]
    [endloop]
    [progress deleting states]
    !drned-xmnr state delete-state state-name dhcp-ls
    ?success Deleted: dhcp-ls$
    !drned-xmnr state delete-state state-name-pattern subnet*
    ?success Deleted: (subnet, subnet2|subnet2, subnet)$
    [invoke check-states "empty time"]
    !drned-xmnr state delete-state state-name time
    ?success Deleted: time$
    [progress walk-state with sets]
    !config dhcp default-lease-time 300s
    !commit
    ?Commit complete
    !drned-xmnr state record-state state-name lt:3
    ?success
    !config dhcp default-lease-time 400s
    !commit
    ?Commit complete
    !drned-xmnr state record-state state-name lt:4
    ?success
    !drned-xmnr transitions walk-states | include "Test transition" | linnum
    [timeout 10]
    -3: Test transition
    """?
    1: Test transition to (empty|lt:[*])
    2: Test transition to (empty|lt:[*])
    admin@ncs\(.*\)\#
    """
    [timeout]

[cleanup]
    [invoke cleanup]
