[config timeout=2000]

[global xmnr_dir=/tmp/xmnr]

[macro shell-check]
    !echo ==$?==
    ?==0==
[endmacro]

[macro ned-test-setup]
    !rm -rf ncs-run
    [progress prepare ncs-run and packages]
    !ncs-project create ncs-run
    [invoke shell-check]
    !cd ncs-run/packages
    [invoke shell-check]
    !ln -s ../../../../ drned-xmnr
    !file drned-xmnr
    ?drned-xmnr: symbolic link to .*
    !file drned-xmnr/python
    ?drned-xmnr/python: directory
    !make -C drned-xmnr/src all
    [invoke shell-check]
[endmacro]

[macro start-ncs-netsim device]
    [progress create and start the netsim]
    !ncs-netsim delete-network
    !ncs-netsim create-network packages/$device 1 $device
    [invoke shell-check]
    !ncs-netsim start
    [invoke shell-check]
    [progress start ncs]
    [timeout 10]
    !ncs --stop; ncs
    [invoke shell-check]
    [timeout]
[endmacro]

[macro test-walk-states states]
    [progress walk states]
    [timeout 15]
    !drned-xmnr transitions walk-states states [ $states ]
    ?Prepare the device
    [loop state $states]
        """?
        Test transition to $state
           load $state
           commit
               (succeeded|\(no modifications\))
           compare config
               succeeded
        """
    [endloop]
    """?
    Device cleanup
       load before-session
       commit
           (succeeded|\(no modifications\))
       compare config
           succeeded
    success Completed successfully
    """
[endmacro]

[macro prepare-test device]
    [invoke prepare-ncs $device]
    [invoke prepare-xmnr true]
[endmacro]

[macro prepare-ncs device]
    [invoke prepare-ncs-tweak $device cat $device]
[endmacro]

[macro prepare-ncs-tweak device tweak tweak_device]
    !ncs_cmd -u admin -c 'mdel /devices/device{$tweak_device}'
    !ncs-netsim --dir ncs-run/netsim ncs-xml-init $device | $tweak | ncs_load -F p -l -m
    [invoke shell-check]
    !ncs_cli -u admin -C
    ???admin@ncs#
    !complete-on-space false
    !config
    !python-vm logging level level-debug
    !drned-xmnr xmnr-log-file output.log
    !commit
    ?Commit complete|No modifications.
    !devices device $tweak_device
    !sync-from
[endmacro]

[macro prepare-xmnr queues]
    [progress setup xmnr]
    !drned-xmnr setup setup-xmnr overwrite true use-commit-queue $queues
    ???success XMNR set up
    [progress record states]
    !drned-xmnr state record-state state-name empty overwrite true
    ???success Recorded state
[endmacro]

[macro cleanup]
    !cd ncs-run
    ?SH-PROMPT:
    !ncs --stop
    ?SH-PROMPT:
    !ncs-netsim stop
    ?SH-PROMPT:
    !make clean
    ?SH-PROMPT:
    !rm -rf packages/*
    ?SH-PROMPT:
[endmacro]
