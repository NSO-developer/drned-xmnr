[doc Testing DrNED-XMNR integration with NCS and DrNED]

[include common.luxinc]

[shell os]
    [invoke ned-test-setup]
    !ncs-make-package --netconf-ned ../../yang ned
    [invoke shell-check]
    [timeout 10]
    !make -C ned/src all
    [invoke shell-check]
    [timeout]
    [progress create and start the netsim]
    !cd ..
    !ncs-netsim delete-network
    !ncs-netsim create-network packages/ned 1 ned
    [invoke shell-check]
    !ncs-netsim start
    [invoke shell-check]
    [progress start ncs]
    [timeout 10]
    !ncs --stop; ncs
    [invoke shell-check]
    [timeout]

[shell ncs-cli]
    [invoke prepare-test dhcp netconf 12022]
    !config dhcp defaultLeaseTime 200s
    !commit
    ???Commit complete.
    !drned-xmnr state record-state state-name time
    ???success
    ~drned-xmnr state import-state-files
    ! file-path-pattern ../subnet.xml merge false overwrite true
    ???success
    !drned-xmnr state check-states validate true
    ???success all states are consistent
    [progress walk states]
    [timeout 15]
    !drned-xmnr transitions walk-states states [ empty subnet time ]
    """?
    Prepare the device
    Test transition to empty
       load empty
       commit
           succeeded
       compare config
           succeeded
    Test transition to subnet
       load subnet
       commit
           succeeded
       compare config
           succeeded
    Test transition to time
       load time
       commit
           succeeded
       compare config
           succeeded
    Device cleanup
       load before-session
       commit
           (succeeded|\(no modifications\))
       compare config
           succeeded
    success Completed successfully
    """

[cleanup]
    [invoke cleanup]
