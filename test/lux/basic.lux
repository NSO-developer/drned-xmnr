[doc Testing DrNED-XMNR integration with NCS and DrNED]

[include common.luxinc]

[macro run-queues queues]
    [my shell=$LUX_SHELLNAME]
[shell ncs-cli]
    !no config
    !commit
    ?Commit complete|No modifications
    [invoke prepare-device-xmnr $queues]
[shell os]
    !rm -f $ned0_log
    !$queues && echo -n en || echo -n dis; echo abled
    ?(disabled|enabled)
    [my qtest=$1]
    !$queues && echo -n dis || echo -n en; echo abled
    ?(disabled|enabled)
    [my neg_qtest=$1]
    !tail -F $ned0_log
    -Commit queues are $neg_qtest
[shell ncs-cli]
    [invoke test-walk-states "empty subnet dhcp-ls time"]
[shell os]
    """?
    -----*
    .* - walk states
    -----*
    """
    ?Commit queues are $qtest
    ?====* 1 passed, .* ====*
    ~$_CTRL_C_
    ?SH-PROMPT:
[shell $shell]
[endmacro]

[macro check-states states]
    [my shell=$LUX_SHELLNAME]
[shell os]
    ~netconf-console --get -x /devices/device/drned-xmnr/state/states |
    ~sed -n 's%.*<state>\(.*\)</state>.*%\1%p' |
    !sort | xargs echo
    """?
    netconf-console --get .*
    $states$$
    SH-PROMPT:
    """
[shell $shell]
[endmacro]

[invoke setup-and-prepare]
[shell ncs-cli]
    !config dhcp default-lease-time 200s
    !commit
    ???Commit complete.
    !drned-xmnr state record-state state-name time format c-style overwrite true
    ???success
    !drned-xmnr state record-state state-name time
    ???failure state time already exists
    ~drned-xmnr state import-state-files
    ! file-path-pattern ../subnet.xml merge false overwrite true
    ???success
    ~drned-xmnr state import-state-files
    ! file-path-pattern ../dhcp-ls.cfg format nso-c-style merge false overwrite true
    ???success
[shell os]
    [local ned0_dir=$xmnr_dir/ned0]
    [local ned0_log=$ned0_dir/test/output.log]
    !cp -v ../subnet2.xml $ned0_dir/test/states/
    ??'../subnet2.xml' -> '$ned0_dir/test/states/subnet2.xml'
    ?SH-PROMPT:
[shell ncs-cli]
    !drned-xmnr state record-state state-name subnet2
    ???failure state subnet2 already exists
    [invoke check-states "dhcp-ls empty subnet subnet2 time"]
    [progress validate states]
    !drned-xmnr state check-states validate true
    ???success all states are consistent
    ?
    [progress view states]
    !drned-xmnr state view-state state-name dhcp-ls
    """?
    drned-xmnr state view-state state-name dhcp-ls
    success <config xmlns="http://tail-f.com/ns/config/1.0">
     *<devices xmlns="http://tail-f.com/ns/ncs">
     *<device>
     *<name>ned0</name>
     *<config>
     *<dhcp xmlns="http://tail-f.com/ns/example/dhcpd">
     *<default-lease-time>PT200S</default-lease-time>
     *</dhcp>
     *</config>
     *</device>
     *</devices>
     *</config>
    """
    [loop queues true false]
        [invoke run-queues $queues]
    [endloop]
    [progress deleting states]
    !drned-xmnr state delete-state state-name dhcp-ls
    ?success Deleted: dhcp-ls$
    !drned-xmnr state delete-state state-name-pattern subnet*
    ?success Deleted: (subnet, subnet2|subnet2, subnet)$
    [invoke check-states "empty time"]
    !drned-xmnr state delete-state state-name time
    ?success Deleted: time$
    [progress walk-state with sets]
    !config dhcp default-lease-time 300s
    !commit
    ?Commit complete
    !drned-xmnr state record-state state-name lt:3
    ?success
    !config dhcp default-lease-time 400s
    !commit
    ?Commit complete
    !drned-xmnr state record-state state-name lt:4
    ?success
    !drned-xmnr transitions walk-states | include "Test transition" | linnum
    [timeout 10]
    -3: Test transition
    """?
    1: Test transition to (empty|lt:[*])
    2: Test transition to (empty|lt:[*])
    admin@ncs\(.*\)\#
    """
    [timeout]

    [progress disable/enable states]
    !drned-xmnr state disable-state state-name-pattern lt:*
    ?success
    !drned-xmnr transitions walk-states | include "Test transition" | linnum
    [timeout 10]
    -2: Test transition
    """?
    1: Test transition to empty
    admin@ncs\(.*\)\#
    """
    [timeout]
    !drned-xmnr state enable-state state-name-pattern lt:[1-3]
    ?success
    [timeout 30]
    !drned-xmnr transitions explore-transitions | include "Found|Starting with" | linnum
    """?
    1: Found 2 states .*
    2: Starting with state (empty|lt:3)
    3: Starting with state (empty|lt:3)
    admin@ncs\(.*\)\#
    """
    [timeout]
    !drned-xmnr state enable-state state-name-pattern *
    ?success

    [progress aborting transitions]
[shell os]
    !tail -f $ned0_log
[shell ncs-cli]
    [timeout 5]
    !drned-xmnr transitions walk-states
    ?Test transition
    ~$_CTRL_C_
    ?Aborted
    ???admin@ncs(config-config)
[shell os]
    ?walk states
    ?KeyboardInterrupt
[shell ncs-cli]
    !drned-xmnr transitions explore-transitions
    ?load
    ~$_CTRL_C_
    ?Aborted
[shell os]
    ?explore transitions
    ?KeyboardInterrupt
[shell ncs-cli]
    !drned-xmnr transitions explore-transitions
    ?Transition 1/.
    ?commit
    ~$_CTRL_C_
    ?Aborted
    [timeout]
[shell os]
    ?explore transitions
    ?KeyboardInterrupt
    ~$_CTRL_C_
    ?SH-PROMPT:

[cleanup]
    [invoke cleanup]
