[config timeout=2000]

[macro shell-check]
    !echo ==$?==
    ?==0==
[endmacro]

[macro ned-test-setup]
    !rm -rf ncs-run
    [progress prepare ncs-run and packages]
    !ncs-project create ncs-run
    [invoke shell-check]
    !cd ncs-run/packages
    [invoke shell-check]
    !ln -s ../../../../ drned-xmnr
    !file drned-xmnr
    ?drned-xmnr: symbolic link to .*
    !file drned-xmnr/python
    ?drned-xmnr/python: directory
    !make -C drned-xmnr/src all
    [invoke shell-check]
[endmacro]

[macro start-ncs-netsim device]
    [progress create and start the netsim]
    !ncs-netsim delete-network
    !ncs-netsim create-network packages/$device 1 $device
    [invoke shell-check]
    !ncs-netsim start
    [invoke shell-check]
    [progress start ncs]
    [timeout 10]
    !ncs --stop; ncs
    [invoke shell-check]
    [timeout]
[endmacro]

[macro test-walk-states states]
    [progress walk states]
    [timeout 15]
    !drned-xmnr transitions walk-states states [ $states ]
    ?Prepare the device
    [loop state $states]
        """?
        Test transition to $state
           load $state
           commit
               succeeded
           compare config
               succeeded
        """
    [endloop]
    """?
    Device cleanup
       load before-session
       commit
           succeeded
       compare config
           succeeded
    success Completed successfully
    """
[endmacro]

[macro prepare-test device]
    !ncs_cmd -u admin -c 'mdel /devices/device{$device}'
    !ncs-netsim --dir ncs-run/netsim ncs-xml-init $device | sed -e 's/10022/12022/' -e 's/cli>/netconf>/' | ncs_load -F p -l -m
    [invoke shell-check]
    !ncs_cli -u admin -C
    ???admin@ncs#
    !complete-on-space false
    !config
    !python-vm logging level level-debug
    !commit
    ?Commit complete|No modifications.
    !devices device $device
    !sync-from
    [progress setup xmnr]
    !drned-xmnr setup setup-xmnr overwrite true
    ???success
    [progress record states]
    !drned-xmnr state record-state state-name empty
    ???success
[endmacro]

[macro cleanup]
    !cd ncs-run
    ?SH-PROMPT:
    !ncs --stop
    ?SH-PROMPT:
    !ncs-netsim stop
    ?SH-PROMPT:
    !make clean
    ?SH-PROMPT:
    !rm -rf packages/*
    ?SH-PROMPT:
[endmacro]
